apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'realm-android'
apply plugin: 'com.getkeepsafe.dexcount'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"

    defaultConfig {
        applicationId "com.bkromhout.balances"
        minSdkVersion 22
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [ 'eventBusIndex' : "com.bkromhout.balance.EventBusIndex" ]
            }
        }
    }

    signingConfigs {
        release {
            def propsFile = rootProject.file('keystore.properties')
            if (propsFile.exists()) {
                def props = new Properties()
                props.load(new FileInputStream(propsFile))
                storeFile = file(props['storeFile'])
                storePassword = props['storePassword']
                keyAlias = props['keyAlias']
                keyPassword = props['keyPassword']
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        // Workaround for https://code.google.com/p/android/issues/detail?id=123771 if we need coverage.
        instrumented.initWith(buildTypes.debug)
        instrumented {
            testCoverageEnabled true
        }

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dexcount {
    includeClasses = true
    includeTotalMethodCount = true
    orderByMethodCount = true
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.android.support:appcompat-v7:25.0.0'
    compile 'com.android.support:support-v4:25.0.0'
    compile 'com.android.support:percent:25.0.0'
    compile 'com.android.support:design:25.0.0'
    compile 'com.android.support:recyclerview-v7:25.0.0'
    compile 'com.android.support:cardview-v7:25.0.0'
    compile 'com.android.support:support-annotations:25.0.0'

    compile 'org.greenrobot:eventbus:3.0.0'
    annotationProcessor 'org.greenrobot:eventbus-annotation-processor:3.0.1'
    compile 'com.jakewharton:butterknife:8.4.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'
    compile 'com.jakewharton.timber:timber:4.3.1'
    compile 'com.afollestad.material-dialogs:core:0.9.1.0'
    compile 'com.wdullaer:materialdatetimepicker:2.5.0'
    compile ('com.bkromhout:realm-recyclerview-lite:3.0.0@aar') {
        transitive = true
    }

    /* Local Tests */
    testCompile 'junit:junit:4.12'

    /* Instrumented Tests */
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}

//android.applicationVariants.all { variant ->
//    variant.variantData.variantConfiguration.javaCompileOptions.annotationProcessorOptions
//        .arguments = ['eventBusIndex' : "com.bkromhout.balance.EventBusIndex"]
//
//}
